buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.bouncycastle:bcpg-jdk15on:1.59'
        classpath 'commons-io:commons-io:2.5'
        classpath 'commons-codec:commons-codec:1.11'
    }
}


import org.apache.commons.codec.digest.Crypt
import org.apache.commons.codec.digest.DigestUtils
import org.apache.commons.io.FileUtils
import org.bouncycastle.asn1.pkcs.PrivateKeyInfo
import org.bouncycastle.asn1.x509.SubjectPublicKeyInfo
import org.bouncycastle.util.io.pem.PemObject
import org.bouncycastle.util.io.pem.PemWriter

import java.nio.ByteBuffer
import java.nio.charset.Charset
import java.security.KeyPair
import java.security.KeyPairGenerator
import java.security.SecureRandom

def generateUser(String username) {
    KeyPairGenerator keyPairGenerator = KeyPairGenerator.getInstance("RSA")
    keyPairGenerator.initialize(2048, new SecureRandom())
    KeyPair keyPair = keyPairGenerator.genKeyPair()

    PrivateKeyInfo pkInfo = PrivateKeyInfo.getInstance(keyPair.private.encoded)
    byte[] privateKeyPKCS1 = pkInfo.parsePrivateKey().toASN1Primitive().encoded
    PemObject pemObject = new PemObject("RSA PRIVATE KEY", privateKeyPKCS1)
    StringWriter stringWriter = new StringWriter()
    PemWriter pemWriter = new PemWriter(stringWriter)
    pemWriter.writeObject(pemObject)
    pemWriter.close()
    String privateKeyString = stringWriter.toString()

    SubjectPublicKeyInfo spkInfo = SubjectPublicKeyInfo.getInstance(keyPair.public.encoded)
    byte[] publicKeyPKCS1 = spkInfo.parsePublicKey().encoded
    pemObject = new PemObject("RSA PUBLIC KEY", publicKeyPKCS1)
    stringWriter = new StringWriter()
    pemWriter = new PemWriter(stringWriter)
    pemWriter.writeObject(pemObject)
    pemWriter.close()
    String publicKeyString = stringWriter.toString()

    String sshKeyString = "ssh-rsa " + Base64.getEncoder().encodeToString(keyPair.public.encoded)

    String password = UUID.randomUUID().toString().replace("-", "")
    String salt = UUID.randomUUID().toString().replace("-", "")
    String hash = Crypt.crypt(password, "\$1\$${salt}\$")

    mkdir ".tmp/users"
    File userYML = new File(".tmp/users/${username}.yml")
    FileUtils.writeLines(userYML, Arrays.asList("---", "password_hash: " + hash, "pubkey: " + sshKeyString))
    mkdir ".tmp/users/pgp"
    File userPub = new File(".tmp/users/pgp/${username}.pub")
    FileUtils.write(userPub, publicKeyString, Charset.defaultCharset())
    File userSec = new File(".tmp/users/pgp/${username}.sec")
    FileUtils.write(userSec, privateKeyString, Charset.defaultCharset())

    return password
}

task clearCEGAConfiguration {
    doLast {
        def result = exec {
            ignoreExitValue = true
            executable "docker"
            args "config", "rm", "cega.defs.json", "cega.rabbitmq.config", "eureka.py", "server.py", "users.html", "john.yml", "jane.yml"
        }
        if (result.exitValue == 0) {
            delete ".tmp"
        }
    }
}

task createCEGAEurekaConfiguration {
    doLast {
        exec {
            workingDir "${project.projectDir.toString()}/../../docker/images/cega"
            ignoreExitValue = true
            executable "docker"
            args "config", "create", "eureka.py", "eureka.py"
        }
    }
}

task createCEGAUsersConfiguration {
    doLast {
        exec {
            workingDir "${project.projectDir.toString()}/../../docker/images/cega"
            ignoreExitValue = true
            executable "docker"
            args "config", "create", "server.py", "server.py"
        }

        exec {
            workingDir "${project.projectDir.toString()}/../../docker/images/cega"
            ignoreExitValue = true
            executable "docker"
            args "config", "create", "users.html", "users.html"
        }

        String johnPassword = generateUser("john")
        def result = exec {
            workingDir ".tmp/users"
            ignoreExitValue = true
            executable "docker"
            args "config", "create", "john.yml", "john.yml"
        }
        if (result.exitValue == 0) {
            File johnTrace = new File(".tmp/trace/john.passwd")
            FileUtils.write(johnTrace, johnPassword, Charset.defaultCharset())
        }
        String janePassword = generateUser("jane")
        result = exec {
            workingDir ".tmp/users"
            ignoreExitValue = true
            executable "docker"
            args "config", "create", "jane.yml", "jane.yml"
        }
        if (result.exitValue == 0) {
            File janeTrace = new File(".tmp/trace/jane.passwd")
            FileUtils.write(janeTrace, janePassword, Charset.defaultCharset())
        }
    }
}

task createCEGAMQConfiguration {
    doLast {
        byte[] saltBytes = new byte[4]
        new SecureRandom().nextBytes(saltBytes)
        String password = UUID.randomUUID().toString().replace("-", "")
        byte[] passwordBytes = password.getBytes()
        byte[] concat = ByteBuffer.allocate(saltBytes.length + passwordBytes.length).put(saltBytes).put(passwordBytes).array()
        byte[] hash = DigestUtils.sha256(concat)
        concat = ByteBuffer.allocate(saltBytes.length + hash.length).put(saltBytes).put(hash).array()
        String saltedHash = Base64.getEncoder().encodeToString(concat)

        mkdir ".tmp/mq"
        File defsJSON = new File(".tmp/mq/defs.json")
        FileUtils.write(defsJSON, "{\"rabbit_version\":\"3.6.11\",\n" +
                " \"users\":[{\"name\":\"lega\",\"password_hash\":\"${saltedHash}\",\"hashing_algorithm\":\"rabbit_password_hashing_sha256\",\"tags\":\"administrator\"}],\n" +
                " \"vhosts\":[{\"name\":\"lega\"}],\n" +
                " \"permissions\":[{\"user\":\"lega\", \"vhost\":\"lega\", \"configure\":\".*\", \"write\":\".*\", \"read\":\".*\"}],\n" +
                " \"parameters\":[],\n" +
                " \"global_parameters\":[{\"name\":\"cluster_name\", \"value\":\"rabbit@localhost\"}],\n" +
                " \"policies\":[],\n" +
                " \"queues\":[{\"name\":\"inbox\",           \"vhost\":\"lega\", \"durable\":true, \"auto_delete\":false, \"arguments\":{}},\n" +
                "           {\"name\":\"inbox.checksums\", \"vhost\":\"lega\", \"durable\":true, \"auto_delete\":false, \"arguments\":{}},\n" +
                "\t   {\"name\":\"files\",           \"vhost\":\"lega\", \"durable\":true, \"auto_delete\":false, \"arguments\":{}},\n" +
                "\t   {\"name\":\"completed\",       \"vhost\":\"lega\", \"durable\":true, \"auto_delete\":false, \"arguments\":{}},\n" +
                "\t   {\"name\":\"errors\",          \"vhost\":\"lega\", \"durable\":true, \"auto_delete\":false, \"arguments\":{}}],\n" +
                " \"exchanges\":[{\"name\":\"localega.v1\", \"vhost\":\"lega\", \"type\":\"topic\", \"durable\":true, \"auto_delete\":false, \"internal\":false, \"arguments\":{}}],\n" +
                " \"bindings\":[{\"source\":\"localega.v1\",\"vhost\":\"lega\",\"destination_type\":\"queue\",\"arguments\":{},\"destination\":\"inbox\",\"routing_key\":\"files.inbox\"},\n" +
                "\t     {\"source\":\"localega.v1\",\"vhost\":\"lega\",\"destination_type\":\"queue\",\"arguments\":{},\"destination\":\"inbox.checksums\",\"routing_key\":\"files.inbox.checksums\"},\n" +
                "\t     {\"source\":\"localega.v1\",\"vhost\":\"lega\",\"destination_type\":\"queue\",\"arguments\":{},\"destination\":\"files\",\"routing_key\":\"files\"},\n" +
                "\t     {\"source\":\"localega.v1\",\"vhost\":\"lega\",\"destination_type\":\"queue\",\"arguments\":{},\"destination\":\"completed\",\"routing_key\":\"files.completed\"},\n" +
                "\t     {\"source\":\"localega.v1\",\"vhost\":\"lega\",\"destination_type\":\"queue\",\"arguments\":{},\"destination\":\"errors\",\"routing_key\":\"files.error\"}]\n" +
                "}", Charset.defaultCharset())

        def result = exec {
            workingDir ".tmp/mq"
            ignoreExitValue = true
            executable "docker"
            args "config", "create", "cega.defs.json", "defs.json"
        }
        if (result.exitValue == 0) {
            File mqTrace = new File(".tmp/trace/mq.passwd")
            FileUtils.write(mqTrace, password, Charset.defaultCharset())
        }

        File rabbitmqConfig = new File(".tmp/mq/rabbitmq.config")
        FileUtils.write(rabbitmqConfig, "%% -*- mode: erlang -*-\n" +
                "%%\n" +
                "[{rabbit,[{loopback_users, [ ] },\n" +
                "\t  {disk_free_limit, \"1GB\"}]},\n" +
                " {rabbitmq_management, [ {load_definitions, \"/etc/rabbitmq/defs.json\"} ]}\n" +
                "].", Charset.defaultCharset())

        exec {
            workingDir ".tmp/mq"
            ignoreExitValue = true
            executable "docker"
            args "config", "create", "cega.rabbitmq.config", "rabbitmq.config"
        }

        File restTrace = new File(".tmp/trace/rest.passwd")
        FileUtils.write(restTrace, UUID.randomUUID().toString().replace("-", ""), Charset.defaultCharset())
    }
}

task createCEGAConfiguration {
    dependsOn clearCEGAConfiguration, createCEGAEurekaConfiguration, createCEGAUsersConfiguration, createCEGAMQConfiguration
}

task deployCEGAStack {
    doLast {
        File restTrace = new File(".tmp/trace/rest.passwd")
        String restPassword = FileUtils.readFileToString(restTrace, Charset.defaultCharset())
        exec {
            executable "docker"
            args "stack", "deploy", "--compose-file", "docker-stack.yml", "cega"
            environment "LEGA_INSTANCES": "lega", "CEGA_REST_lega_PASSWORD": restPassword
        }
    }
}

task removeCEGAStack {
    doLast {
        exec {
            executable "docker"
            args "stack", "rm", "cega"
        }
    }
}